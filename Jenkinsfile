// This Jenkinsfile is used by Jenkins to run the 'ChEBI Update' step of Reactome's release.
// This step synchronizes Reactome's database with ChEBI. 

import org.reactome.release.jenkins.utilities.Utilities

// Shared library maintained at 'release-jenkins-utils' repository.
def utils = new Utilities()

pipeline {
	agent any

	environment {
		ECR_URL = 'public.ecr.aws/reactome/release-chebi-update'
		CONT_NAME = 'chebi_container'
		CONT_ROOT = '/opt/release-chebi-update'
	}

	stages {
		// This stage checks that an upstream step, GO Update, was run successfully.
		stage('Check GO Update build succeeded'){
			steps{
				script{
					utils.checkUpstreamBuildsSucceeded("ConfirmReleaseConfigs")
				}
			}
		}

		/*
		// This stage backs up the gk_central database before it is modified.
		stage('Setup: Back up gk_central before modifications'){
			steps{
				script{
					withCredentials([usernamePassword(credentialsId: 'mySQLCuratorUsernamePassword', passwordVariable: 'pass', usernameVariable: 'user')]){
						utils.takeDatabaseDumpAndGzip("${env.GK_CENTRAL_DB}", "chebi_update", "before", "${env.CURATOR_SERVER}")
					}
				}
			}
		}
		*/
		// This stage makes the config/credentials file
		stage('Setup: Get config/credentials'){
			steps{
				script{
					withCredentials([file(credentialsId: 'Config', variable: 'ConfigFile')]){
						sh "mkdir -p config"
						sh "sudo cp $ConfigFile config/auth.properties"
						sh "sudo chown jenkins:jenkins config/ -R"                    
					}
				}
			}
		}
		// This stage pulls the docker image and removes old containers
		stage('Setup: Pull and clean docker environment'){
			steps{
				sh "docker pull ${ECR_URL}:latest"
				sh """
					if docker ps -a --format '{{.Names}}' | grep -Eq '${CONT_NAME}'; then
						docker rm -f ${CONT_NAME}
					fi
				"""
			}
		}

		// This stage executes the ChEBI Update jar file via docker. 
		stage('Main: ChEBI Update'){
			steps{
				sh """\
					docker run -v \$(pwd)/config:${CONT_ROOT}/config --net=host --name ${CONT_NAME} \\
						${ECR_URL}:latest \\
						/bin/bash -c 'java -Xmx${env.JAVA_MEM_MAX}m -jar target/chebi-update-*-jar-with-dependencies.jar config/auth.properties'
				"""
			}
		}

		// This stage backs up the gk_central database after modification.
		stage('Post: Backup gk_central after modifications'){
			steps{
				script{
					withCredentials([usernamePassword(credentialsId: 'mySQLCuratorUsernamePassword', passwordVariable: 'pass', usernameVariable: 'user')]){
						utils.takeDatabaseDumpAndGzip("${env.GK_CENTRAL_DB}", "chebi_update", "after", "${env.CURATOR_SERVER}")
					}
				}
			}
		}
		// This stage archives the report files generated by ChEBI Update and sends them in an email to the default recipients list.
		stage('Post: Email ChEBI Update Reports'){
			steps{
				script{
					def releaseVersion = utils.getReleaseVersion()
					def chebiUpdateReportsFile = "chebi-update-v${releaseVersion}-reports.tgz"
					
					sh "mkdir -p logs reports"
					sh "docker cp ${CONT_NAME}:${CONT_ROOT}/logs/. logs/"
					sh "mv logs/*.tsv reports/"
					sh "tar zcf ${chebiUpdateReportsFile} reports/"
					
					def emailSubject = "ChEBI Update Reports for v${releaseVersion}"
					def emailBody = "Hello,\n\nThis is an automated message from Jenkins regarding an update for ${releaseVersion}. The ChEBI Update step has completed. Please review the reports attached to this email. If they look correct, these reports need to be uploaded to the Reactome Drive at Reactome>Release>Release QA>${releaseVersion}_QA>V${releaseVersion}_QA_ChEBI_Update_Reports. The URL to the new V${releaseVersion}_QA_ChEBI_Update_Reports folder also needs to be updated at https://devwiki.reactome.org/index.php/Reports_Archive under 'ChEBI Update Reports'. Please add the older ChEBI report URL to the 'Archived reports' section of the page. If they don't look correct, please email the developer running Release. \n\nThanks!"
					utils.sendEmailWithAttachment("${emailSubject}", "${emailBody}", "${chebiUpdateReportsFile}")
				}
			}
		}
		// All databases, logs, and data files generated by this step are compressed before moving them to the Reactome S3 bucket. All files are then deleted.
		stage('Post: Archive Outputs'){
			steps{
				script{
					def releaseVersion = utils.getReleaseVersion()
					def dataFiles = ["chebi-update-v${releaseVersion}-reports.tgz"]
					// ChEBI Update log files are already in a folder called 'logs', so an empty list is passed.
					def logFiles = []
					def foldersToDelete = []
					utils.cleanUpAndArchiveBuildFiles("chebi_update", dataFiles, logFiles, foldersToDelete)
				}
			}
		}	
	}
}